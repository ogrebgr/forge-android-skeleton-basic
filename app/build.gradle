apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"


    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }

    // Release signing configuration is moved intentionally to external file so it
    // will not get accidentally uploaded to github or other external repo.
    // Location of the external signing configuration file is specified in local.properties
    // See signing-config.gradle.example for example content of the signing config.
    if(project.hasProperty("MyProject.signing")
            && new File(project.property("MyProject.signing") + ".gradle").exists()) {
        apply from: project.property("MyProject.signing") + ".gradle";
    }

    defaultConfig {
        applicationId "com.bolyartech.forge.skeleton.dagger.basic"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
//        disable 'MissingTranslation'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile project(':forge_android')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup:otto:1.3.6'
    compile 'com.google.code.gson:gson:2.4'


    compile('com.github.tony19:logback-android-core:1.1.1-4') {
        exclude group: 'ch.qos.logback.core.net'
    }
    compile('com.github.tony19:logback-android-classic:1.1.1-4') {
        exclude group: 'ch.qos.logback.core.net'
    }

    apt 'com.google.dagger:dagger-compiler:2.0'
    androidTestApt 'com.google.dagger:dagger-compiler:2.0'
    compile 'com.google.dagger:dagger:2.0'

    compile 'com.google.android.gms:play-services-gcm:8.3.0'
    compile 'com.google.android.gms:play-services-identity:8.3.0'
    compile 'com.facebook.android:facebook-android-sdk:4.6.0'
    compile 'ch.acra:acra:4.7.0'
}
